


01. What is PL/SQL Strong Ref Cursors?
02. Can we use Strong Ref Cursor with any SELECT statement?
03. Can we use any PL/SQL datatypes for declaring our Strong Ref Cursor?
04. Example of Strong Ref Cursors With Table Based Record Datatype.
05. What/Why PL/SQL Weak Ref Cursor?
06. Can we use Weak Ref Cursor with any SELECT statement or not?
07. How to create Weak Ref Cursor?
08. What is PL/SQL SYS_REFCURSOR?
09. How will it being a pre-defined ref cursor helps us?
10. How to create PL/SQL SYS_REFCURSOR?






Strong Ref Cursors With Table Based Record Datatype:
http://www.rebellionrider.com/strong-ref-cursors-with-table-based-record-datatype/

01. What is PL/SQL Strong Ref Cursors?
----------------------------------
A ref cursor which has fixed return type is called a Strong Ref Cursor in Oracle Database. 
Because of fixed return type, strong ref cursors can only be used selectively. 
For instance with those SELECT statements that return the result whose datatype matches with the one that you have fixed during cursor’s declaration.

02. Can we use Strong Ref Cursor with any SELECT statement?
-------------------------------------------------------
No, we cannot use Strong Ref Cursors with any SELECT statement. This is because of the fixed ‘Return Type’.
Nonetheless it can only be used with those SELECT statements that return the result whose datatype matches with the ‘Return Clause’ of the cursor.

03. Can we use any PL/SQL datatypes for declaring our Strong Ref Cursor?
-------------------------------------------------------------------
No, we cannot. Moreover, the return type of a strong ref cursor must always be a Record Datatype.  
It can either be a Table Based Record datatype or a User Defined Record Datatype.

04. Example of Strong Ref Cursors With Table Based Record Datatype
---------------------------------------------------------------
A Strong Ref Cursor with Table Based Record Datatype:

 SET SERVEROUTPUT ON
 DECLARE
    	/*Create Ref Pointer Type*/
	TYPE	my_RefCur	IS REF CURSOR RETURN 	employees%ROWTYPE;
	/*Create Cursor Variable*/
	cur_var my_RefCur;
	rec_var     employees%ROWTYPE;
 BEGIN
	OPEN cur_var FOR SELECT * FROM employees WHERE employee_id = 100;
	FETCH cur_var INTO rec_var;
	CLOSE cur_var;
	DBMS_OUTPUT.PUT_LINE ('Employee '||rec_var.first_name||' has salary '||rec_var.salary||'.');
END;

PL/SQL Strong Ref Cursor With User Defined Record Datatype:
===========================================================
http://www.rebellionrider.com/pl-sql-strong-ref-cursor-with-user-defined-record-datatype/

Example: PL/SQL Strong Ref Cursor with User Defined Record Datatype Variable.
Create a strong ref cursor with the SELECT statement. Which returns only the salary of the employee whose employee id is 100.

SET SERVEROUTPUT ON;
DECLARE
	--Create User-Defined Record Datatype
    TYPE my_rec IS RECORD (
        emp_sal employees.salary%TYPE
        );
	--Declare Strong Ref Cursor
    TYPE RefCur IS REF CURSOR RETURN my_rec;
    cur_var REFCUR;
	--Another anchored datatype variable for holding data
    at_var  employees.salary%TYPE;
BEGIN
   OPEN cur_var FOR SELECT salary FROM employees WHERE employee_id = 100;
    FETCH cur_var INTO at_var;
    CLOSE cur_var;
    DBMS_OUTPUT.PUT_LINE ('Salary of the employee is '||at_var);
END;

=============================WEAK REF. CURSOR=========================
http://www.rebellionrider.com/how-to-create-pl-sql-weak-ref-cursor-in-oracle-database/


05. What/Why PL/SQL Weak Ref Cursor?
---------------------------

A ref cursor which does not have a fixed return type is called a Weak Ref Cursor.

In the previous tutorial we learnt how to fetch data of different datatypes from different columns using the strong ref cursor. 
The strong ref cursors have a limitation that they have fixed return type which can only be a record datatype.
However fixed return type makes a strong ref cursor less error prone in the application development. 
But this definitely requires an extra step of creating a user defined record datatype.

So, don’t we have any alternative way of fetching data of different datatype using ref cursor which doesn’t require the creation of user defined record datatype? 
Can’t we create a ref cursor which doesn’t have a fixed return type? Let’s find out the answers of these questions in this blog.

06. Can we use Weak Ref Cursor with any SELECT statement or not?
------------------------------------------------------------
Yes, we can. Because weak ref cursors don’t have a fixed return type like strong ref cursors thus they are open to all types of SELECT statements. 
This also makes them most frequently used ref cursors in comparison to others.


07. How to create Weak Ref Cursor?
------------------------------
SET SERVEROUTPUT ON;
DECLARE
    /*Declare Weak Ref Cursor*/
    TYPE wk_RefCur IS REF CURSOR;
    /*Declare Cursor Variable of ref cursor type*/
    cur_var wk_RefCur;
    
     /*Declare two "Anchored Datatype Variable" for holding data from the cursor*/
    f_name  employees.first_name%TYPE;
    emp_sal employees.salary%TYPE;
BEGIN
    OPEN cur_var FOR SELECT first_name, Salary FROM employees WHERE employee_id = 100;
    FETCH cur_var INTO f_name, emp_sal;
    CLOSE cur_var;
    DBMS_OUTPUT.PUT_LINE (f_name ||' '||emp_sal);
END;


=================== SYS_REFCURSOR =======================
http://www.rebellionrider.com/how-to-create-pl-sql-sys_refcursor-in-oracle-database/

08. What is PL/SQL SYS_REFCURSOR?
------------------------------
SYS_REFCURSOR is a predefined weak ref cursor which comes built-in with the Oracle database software.

09. How will it being a pre-defined ref cursor helps us?
----------------------------------------------------
SYS_REFCURSOR is a predefined weak ref cursor which means we do not have to define it into our code like we did with the explicit weak ref cursor 
in the previous tutorial. As we learnt in that tutorial that creating a weak ref cursor is a two-step process. 
First you have to create the “weak ref cursor type” and then you have to create “a cursor variable” using that ref cursor.

When using SYS_REFCURSOR, you just have to create a cursor variable and nothing else. 
SYS_REFCURSOR completely minimizes the first step where you create the weak ref cursor type.

So Sys_Refcursor is a built-in ref cursor, where can we find its definition?
You can find out the definition of PL/SQL SYS_REFCURSOR in a script named “stdspec.sql”. You can locate this script into the directory whose path is as follow. 


Why would someone declare a weak ref cursor when we already have a predefined one?
----------------------------------------------------------------------------------
The answer to this question is No, we do not need to create a weak ref cursor explicitly. 
Oracle minimized the need of creating a weak ref cursor by shipping SYS_REFCURSOR as a part of standard package 
since Oracle Database 9i though the option of creating one is still available.

It is like a personal choice, if someone wants to create a weak ref cursor then they can. 
If not, then they have SYS_REFCURSOR always at their disposal. 



10. How to create PL/SQL SYS_REFCURSOR?
------------------------------------
SET SERVEROUTPUT ON;
DECLARE
    --Declare cursor variable of SYS_REFCURSOR type
    cur_var SYS_REFCURSOR;
    
    --Declare variables for holding data
    f_name  employees.first_name%TYPE;
    emp_sal employees.salary%TYPE;
BEGIN
OPEN cur_var FOR SELECT first_name, Salary FROM employees WHERE employee_id = 100;
    FETCH cur_var INTO f_name, emp_sal;
    CLOSE cur_var;
    DBMS_OUTPUT.PUT_LINE(f_name ||' '||emp_sal);
END;


